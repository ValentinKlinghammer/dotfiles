" Configuration file for VIM
" Author: valentin@quelltextfabrik.de
" Created: 04.06.2013

" Remove all autocmds, so they are not loaded twice
if has("autocmd")
  autocmd!
endif

set nocompatible          " Enable all the cool stuff
scriptencoding utf-8
set encoding=utf-8

" --- Environment ----------------------------------------------------------
" Vim should use the XDG Config paths
set directory=$XDG_CACHE_HOME/vim,~/,/tmp
set backupdir=$XDG_CACHE_HOME/vim,~/,/tmp
set viminfo+=n$XDG_CACHE_HOME/vim/viminfo
set runtimepath=$XDG_CONFIG_HOME/vim,$XDG_CONFIG_HOME/vim/after,$VIM,$VIMRUNTIME
let $MYVIMRC="$XDG_CONFIG_HOME/vim/vimrc"

" --- Key mapping ----------------------------------------------------------
" Remap leader to the easier reachable ,
let mapleader=","

" Paste from system clipboard
nmap <Leader>p "+p
" TODO Yank to system clipboard?

" Clear search highlighting
nmap <Leader>q :nohlsearch<CR>

" Switch buffer more easily
nmap <C-e> :b#<CR>
nmap <C-l> :bnext<CR>
nmap <C-h> :bprev<CR>

" --- Options  ---------------------------------------------------------------
" --- System
set lazyredraw  " Don't redraw when don't have to
set ttyfast     " Using a terminal emulator

" --- Tab and whitespace options
set smarttab                      " TODO find out what that setting does
set tabstop=2                     " Default tab stops
set shiftwidth=2                  " Default auto indent
set softtabstop=2
set expandtab                     " Use spaces instead of tabs
set autoindent                    " Use indent from previous line

set backspace=indent,eol,start    " If backspace was any smarter it'd send a terminator back in time

" --- Display options
set number                                " Display line numbers
set cursorline                            " Highlight current line
set cul cuc                               " Cursor crosshair

set nowrap                                " Soft wrapping, without changing text
set linebreak                             " Only break on characters in 'breakat'
if has('multi_byte')                      " Use fancy symbols to indicate linebreak
  let &showbreak = '↳ '
else
  let &showbreak = '> '
endif

set showmatch                             " Show matching bracket
set matchpairs=(:),[:],{:}                " Expected pairs

set laststatus=2                          " Always show status line
set cmdheight=2                           " Command line two lines high
set scrolloff=5                           " Keep at least 5 lines above/below
set sidescrolloff=5                       " Keep at least 5 lines left/right

set splitright                            " Split right instead of left
set splitbelow                            " Split below

set list listchars=tab:»·,trail:·,nbsp:+  " Show the leading whitespaces"
set display+=uhex                         " Show unprintables as <xx>
set display+=lastline                     " Show as much as possible of the last line

" --- Search options
set ignorecase    " Ignore case when searching
set smartcase     " Except when uppercase is used
set incsearch     " Show matches live while typing
set hlsearch      " Highlight all search matches, turn off with :noh

" --- Auto complete options
set wildmode=list:longest,full  " TODO document the awesomeness of this
set wildmenu
set wildignore+=*.o,*.obj,*.pyc,*.pyo,*.pyd,*.class,*.lock
set wildignore+=*.png,*.gif,*.jpg,*.ico
set wildignore+=',.svn,.hg
set showcmd                     " Display incomplete commands

" --- Buffer and backup options
set hidden                " Hide buffer instead of unloading when abandoning it
set history=1000          " Keep more history of : commands
set undolevels=1000       " 1000 undos
set updatecount=100       " Switch swap file every 160 chars

set autoread              " Reload file in VIM if changed outside and not changed in VIM

" Create a tmp folder in the home directory for swap, backup and undo files
if isdirectory($HOME . '/tmp') == 0
  :silent !mkdir -p ~/tmp > /dev/null 2>&1
endif

set backupdir=~/tmp
set backup
set directory=~/tmp
set swapfile
if exists("+undofile")
  set undodir=~/tmp
  set undofile
endif

" --- Vundler ----------------------------------------------------------------
" This section should setup VIM with very little interaction, vundle and
" the specified Bundles are installed autmatically

" --- Function to install bundles automagically
function! LoadBundles()
  " --- let Vundle manage Vundle
  Bundle 'gmarik/vundle'

  " --- CtrlP plugin for fuzzy file finding
  Bundle 'kien/ctrlp.vim'
  let g:ctrlp_map = '<c-p>'
  let g:ctrlp_cmd = 'CtrlP'
  let g:ctrlp_custom_ignore = {
      \ 'dir': '\v[\/](\.git|\.hg|\.svn|node_modules|CVS|tmp|Library|Applications|dist|build|docs|Music|[^\/]*-store)$',
      \ 'file': '\v\.(exe|so|dll)$',
      \
      \ }
  let g:ctrlp_user_command = {
      \ 'types' : {
      \ 1: ['.git', 'cd %s && git ls-files . --cached --exclude-standard --others'],
      \ 2: ['.hg', 'hg --cwd %s locate -I .'],
      \
      \ }
      \ }

  " --- File tree view
  Bundle 'scrooloose/nerdtree'

  " --- Fast autocompletion
  "Bundle 'Valloric/YouCompleteMe'
  "if !filereadable(expand('~/.vim/bundle/YouCompleteMe/python/ycm_core.so'))
  "  call system('cd ~/.vim/bundle/YouCompleteMe && ./install.sh')
  "endif

  " --- Vim Lightline for a nicer status bar
  Bundle 'itchyny/lightline.vim'
  set noshowmode
  let g:lightline = {
      \ 'colorscheme': 'wombat',
      \ 'component': {
      \   'readonly': '%{&readonly?"⭤":""}',
      \ },
      \ 'separator': { 'left': '⮀', 'right': '⮂' },
      \ 'subseparator': { 'left': '⮁', 'right': '⮃' }
      \ }

  " --- Changes surrounding brackets or quotes
  Bundle "tpope/vim-surround"

  " --- Make repeat possible for plugin actions, e.g. vim-surround
  Bundle 'tpope/vim-repeat'

  " --- Detect indentation from current file or similiar files
  Bundle 'tpope/vim-sleuth'

  " --- Smart pairing of brackets with CR and SPACE awareness
  Bundle 'jiangmiao/auto-pairs'

  "--- Snippet
  Bundle 'SirVer/ultisnips'
  Bundle 'honza/vim-snippets'

  " Trigger configuration. Do not use <tab> if you use https://github.com/Valloric/YouCompleteMe.
  let g:UltiSnipsExpandTrigger="<tab>"
  let g:UltiSnipsJumpForwardTrigger="<tab>"
  let g:UltiSnipsJumpBackwardTrigger="<s-tab>"

  " If you want :UltiSnipsEdit to split your window.
  let g:UltiSnipsEditSplit="vertical"

  " --- Syntax checking
  " if exists('*getmatches')
  "   Bundle 'scrooloose/syntastic'
  "   let g:syntastic_error_symbol='✗'
  "   let g:syntastic_warning_symbol='⚠'
  " endif
  "
  " --- Toggle comments
  Bundle 'tomtom/tcomment_vim'

  " -- Zen coding, defaults to <C-y>,
  Bundle 'mattn/emmet-vim'

  " --- Some language specific plugins
  " --- Ruby
  " --- Ends e.g. ruby blocks automatically
  Bundle 'tpope/vim-endwise'
  " --- Jump with % from keyword to corresponding end
  " Bundle 'vim-scripts/ruby-matchit'
  " Bundle 'vim-ruby/vim-ruby'
  " Bundle 'tpope/vim-rails'
  Bundle 'wavded/vim-stylus'
  Bundle 'digitaltoad/vim-jade'

  " --- Javascript
  " Bundle 'marijnh/tern_for_vim'
  " Bundle 'mustache/vim-mustache-handlebars'

  " --- Frontend stuff
  Bundle 'tpope/vim-haml'

  " --- Additional functions
  " Turn .todo and .TODO files into todo lists
  " Bundle 'Gorgoroth/NotSoPlainTasks'
  " Bundle 'Gorgoroth/vim-quelltextfabrik-theme'

  " --- Themes
  " Bundle 'altercation/solarized'
  Bundle 'dsolstad/vim-wombat256i'
  Bundle 'jnurmine/Zenburn'
  " Bundle 'ajh17/Spacegray.vim'

  " --- See CSS colors
"   Bundle 'skammer/vim-css-color'
"   Bundle 'vim-scripts/css_color.vim'

  if filereadable(expand("~/.vimrc.bundles"))
    source ~/.vimrc.bundles
  endif
endfunction

" --- Install Vundle and bundles if possible
filetype off                            " required!
if executable("git")
  if !isdirectory(expand("~/.vim/bundle/vundle"))
    echomsg "***************************"
    echomsg "Installing Vundle"
    echomsg "***************************"
    !mkdir -p ~/.vim/bundle && git clone git://github.com/gmarik/vundle.git ~/.vim/bundle/vundle
    let s:bootstrap=1
  endif

  set rtp+=~/.vim/bundle/vundle/
  call vundle#rc()
  call LoadBundles()

  if exists("s:bootstrap") && s:bootstrap
    unlet s:bootstrap
    BundleInstall
    quit
  endif
endif

syntax on
filetype plugin indent on               " required!

" --- Plugin key mappings ----------------------------------------------------
" If CtrlP installed, map that
if exists(":CtrlPBuffer")
  nmap ; :CtrlPBuffer<CR>
endif

" Toggle nerd tree
map <F2> :NERDTreeToggle<CR>

" --- Eyecandy ---------------------------------------------------------------
" --- Color and color scheme
set t_Co=256                  " Enable 256 colors
set background=dark           " Prefer dark background
colorscheme zenburn

if has('gui_running')
  set guifont=UbuntuMono-R-Powerline 12
endif

" --- Helpers ----------------------------------------------------------------
" --- Always jump to last known position if valid
if has ("autocmd")
  autocmd BufReadPost *
    \ if line("'\"") > 0 && line("'\"") <= line("$") |
    \   exe "normal! g`\"" |
    \ endif
endif

" --- Strip trailing whitespace
function! StripTrailingWhite()
  let l:winview = winsaveview()
  silent! %s/\s\+$//
  call winrestview(l:winview)
endfunction
if has("autocmd")
  autocmd BufWritePre *  call StripTrailingWhite()
endif

" --- Map keys
nnoremap <silent> <tab> :wincmd l<CR>
nnoremap <silent> <S-tab> :wincmd h<CR>

" --- Syntax specific settings -----------------------------------------------
" --- Ruby
if has("autocmd")
  autocmd FileType ruby,eruby setlocal cinwords=do
  autocmd FileType ruby,eruby let g:rubycomplete_buffer_loading=1
  autocmd FileType ruby,eruby let g:rubycomplete_rails = 1
  autocmd FileType ruby,eruby let g:rubycomplete_classes_in_global=1
endif

" --- Finish up --------------------------------------------------------------
set secure
" EOF
